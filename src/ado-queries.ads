-----------------------------------------------------------------------
--  ado-queries -- Database Queries
--  Copyright (C) 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2017, 2018, 2019 Stephane Carrez
--  Written by Stephane Carrez (Stephane.Carrez@gmail.com)
--
--  Licensed under the Apache License, Version 2.0 (the "License");
--  you may not use this file except in compliance with the License.
--  You may obtain a copy of the License at
--
--      http://www.apache.org/licenses/LICENSE-2.0
--
--  Unless required by applicable law or agreed to in writing, software
--  distributed under the License is distributed on an "AS IS" BASIS,
--  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
--  See the License for the specific language governing permissions and
--  limitations under the License.
-----------------------------------------------------------------------

with Util.Strings;
with Util.Refs;
with ADO.SQL;

private with Interfaces;
private with ADO.Configs;
with Ada.Strings.Unbounded;
with Ada.Finalization;

--  == Named Queries ==
--  Ada Database Objects provides a small framework which helps in
--  using complex SQL queries in an application by using named queries.
--  The benefit of the framework are the following:
--
--    * The SQL query result are directly mapped in Ada records,
--    * It is easy to change or tune an SQL query without re-building the application,
--    * The SQL query can be easily tuned for a given database.
--
--  The database query framework uses an XML query file:
--
--    * The XML query file defines a mapping that represents the result of SQL queries,
--    * The XML mapping is used by Dynamo code generator to generate an Ada record,
--    * The XML query file also defines a set of SQL queries, each query being
--      identified by a unique name,
--    * The XML query file is read by the application to obtain the SQL query
--      associated with a query name,
--    * The application uses the `List` procedure generated by Dynamo.
--
--  === XML Query File ===
--  The XML query file uses the `query-mapping` root element.  It should
--  define at most one `class` mapping and several `query` definitions.
--  The `class` definition should come first before any `query` definition.
--
--    <query-mapping>
--       <class>...</class>
--       <query>...</query>
--    </query-mapping>
--
--  === SQL Result Mapping ===
--  The XML query mapping is very close to the database XML table mapping.
--  The difference is that there is no need to specify any table name
--  nor any SQL type.  The XML query mapping is used to build an Ada
--  record that correspond to query results.  Unlike the database table mapping,
--  the Ada record will not be tagged and its definition will expose all the record
--  members directly.
--
--  The following XML query mapping:
--
--    <query-mapping>
--      <class name='Samples.Model.User_Info'>
--        <property name="name" type="String">
--           <comment>the user name</comment>
--        </property>
--        <property name="email" type="String">
--           <comment>the email address</comment>
--        </property>
--      </class>
--    </query-mapping>
--
--  will generate the following Ada record and it will instantiate the Ada container `Vectors`
--  generic to provide a support for vectors of the record:
--
--    package Samples.Model is
--       type User_Info is record
--         Name  : Unbounded_String;
--         Email : Unbounded_String;
--       end record;
--       package User_Info_Vectors is
--          new Ada.Containers.Vectors (Index_Type   => Natural,
--                                      Element_Type => User_Info,
--                                      "="          => "=");
--       subtype User_Info_Vector is User_Info_Vectors.Vector;
--    end Samples.Model;
--
--  A `List` operation is also generated and can be used to execute an SQL query and have
--  the result mapped in the record.
--
--  The same query mapping can be used by different queries.
--
--  After writing or updating a query mapping, it is necessary to launch the
--  Dynamo code generator to generate the corresponding Ada model.
--
--  === SQL Queries ===
--  The XML query file defines a list of SQL queries that the application
--  can use.  Each query is associated with a unique name.  The application
--  will use that name to identify the SQL query to execute.  For each query,
--  the file also describes the SQL query pattern that must be used for
--  the query execution.
--
--    <query-mapping>
--      <query name='user-list' class='Samples.Model.User_Info'>
--       <sql driver='mysql'>
--         SELECT u.name, u.email FROM user AS u
--       </sql>
--       <sql driver='sqlite'>
--          ...
--       </sql>
--       <sql-count driver='mysql'>
--          SELECT COUNT(*) FROM user AS u
--       </sql-count>
--      </query>
--    </query-mapping>
--
--  The query contains basically two SQL patterns.  The `sql` element represents
--  the main SQL pattern.  This is the SQL that is used by the `List` operation.
--  In some cases, the result set returned by the query is limited to return only
--  a maximum number of rows.  This is often use in paginated lists.
--
--  The `sql-count` element represents an SQL query to indicate the total number
--  of elements if the SQL query was not limited.
--
--  The `sql` and `sql-count` XML element can have an optional `driver` attribute.
--  When defined, the attribute indicates the database driver name that is specific
--  to the query.  When empty or not defined, the SQL is not specific to a database driver.
--
--  For each query, the Dynamo code generator generates a query definition instance which
--  can be used in the Ada code to be able to use the query.  Such instance is static and
--  readonly and serves as a reference when using the query.  For the above query,
--  the Dynamo code generator generates:
--
--    package Samples.User.Model is
--       Query_User_List : constant ADO.Queries.Query_Definition_Access;
--    private
--       ...
--    end Samples.User.Model;
--
--  When a new query is added, the Dynamo code generator must be launched to update
--  the generated Ada code.
--
--  === Using Named Queries ===
--  In order to use a named query, it is necessary to create a query context instance
--  and initialize it.  The query context holds the information about the query definition
--  as well as the parameters to execute the query.  It provides a `Set_Query` and
--  `Set_Count_Query` operation that allows to configure the named query to be executed.
--  It also provides all the `Bind_Param` and `Add_Param` operations to allow giving the
--  query parameters.
--
--    with ADO.Sessions;
--    with ADO.Queries;
--    ...
--      Session : ADO.Sessions.Session := Factory.Get_Session;
--      Query   : ADO.Queries.Context;
--      Users   : Samples.User.Model.User_Info_Vector;
--      ...
--         Query.Set_Query (Samples.User.Model.Query_User_List);
--         Samples.User.Model.List (Users, Session, Query);
--
--  To use the `sql-count` part of the query, you will use the `Set_Count_Query`
--  with the same query definition.  You will then create a query statement from the
--  named query context and run the query.  Since the query is expected to contain exactly
--  one row, you can use the `Get_Result_Integer` to get the first row and column result.
--  For example:
--
--    Query.Set_Count_Query (Samples.User.Model.Query_User_List);
--    ...
--    Stmt : ADO.Statements.Query_Statement
--      := Session.Create_Statement (Query);
--    ...
--    Stmt.Execute;
--    ...
--    Count : Natural := Stmt.Get_Result_Integer;
--
--  You may also use the `ADO.Datasets.Get_Count` operation which simplifies these steps
--  in:
--
--    Query.Set_Count_Query (Samples.User.Model.Query_User_List);
--    ...
--    Count : Natural := ADO.Datasets.Get_Count (Session, Query);
--
package ADO.Queries is

   --  Exception raised when a query does not exist or is empty.
   Query_Error : exception;

   type Query_Index is new Natural;

   type File_Index is new Natural;

   type Query_File is limited private;
   type Query_File_Access is access all Query_File;

   type Query_Definition is limited private;
   type Query_Definition_Access is access all Query_Definition;

   type Query_Manager is limited new Ada.Finalization.Limited_Controlled with private;
   type Query_Manager_Access is access all Query_Manager;

   --  ------------------------------
   --  Query Context
   --  ------------------------------
   --  The <b>Context</b> type holds the necessary information to build and execute
   --  a query whose SQL pattern is defined in an XML query file.
   type Context is new ADO.SQL.Query with private;

   --  Set the query definition which identifies the SQL query to execute.
   --  The query is represented by the <tt>sql</tt> XML entry.
   procedure Set_Query (Into  : in out Context;
                        Query : in Query_Definition_Access);

   --  Set the query count definition which identifies the SQL query to execute.
   --  The query count is represented by the <tt>sql-count</tt> XML entry.
   procedure Set_Count_Query (Into  : in out Context;
                              Query : in Query_Definition_Access);

   --  Set the query to execute as SQL statement.
   procedure Set_SQL (Into : in out Context;
                      SQL  : in String);

   procedure Set_Query (Into  : in out Context;
                        Name  : in String);

   --  Set the limit for the SQL query.
   procedure Set_Limit (Into  : in out Context;
                        First : in Natural;
                        Last  : in Natural);

   --  Get the first row index.
   function Get_First_Row_Index (From : in Context) return Natural;

   --  Get the last row index.
   function Get_Last_Row_Index (From : in Context) return Natural;

   --  Get the maximum number of rows that the SQL query can return.
   --  This operation uses the <b>sql-count</b> query.
   function Get_Max_Row_Count (From : in Context) return Natural;

   --  Get the SQL query that correspond to the query context.
   function Get_SQL (From    : in Context;
                     Manager : in Query_Manager'Class) return String;

   function Get_SQL (From      : in Query_Definition_Access;
                     Manager   : in Query_Manager;
                     Use_Count : in Boolean) return String;

private

   subtype Driver_Index is ADO.Configs.Driver_Index;

   --  ------------------------------
   --  Query Definition
   --  ------------------------------
   --  The <b>Query_Definition</b> holds the SQL query pattern which is defined
   --  in an XML query file.  The query is identified by a name and a given XML
   --  query file can contain several queries.  The Dynamo generator generates
   --  one instance of <b>Query_Definition</b> for each query defined in the XML
   --  file.  The XML file is loaded during application initialization (or later)
   --  to get the SQL query pattern.  Multi-thread concurrency is achieved by
   --  the Query_Info_Ref atomic reference.
   type Query_Definition is limited record
      --  The query name.
      Name   : Util.Strings.Name_Access;

      --  The query file in which the query is defined.
      File   : Query_File_Access;

      --  The next query defined in the query file.
      Next   : Query_Definition_Access;

      --  The SQL query pattern (initialized when reading the XML query file).
      --  Query  : Query_Info_Ref_Access;
      Query  : Query_Index := 0;
   end record;

   --  ------------------------------
   --  Query File
   --  ------------------------------
   --  The <b>Query_File</b> describes the SQL queries associated and loaded from
   --  a given XML query file.  The Dynamo generator generates one instance of
   --  <b>Query_File</b> for each XML query file that it has read.  The Path,
   --  Sha1_Map, Queries and Next are initialized statically by the generator (during
   --  package elaboration).
   type Query_File is limited record
      --  Query relative path name
      Name          : Util.Strings.Name_Access;

      --  The SHA1 hash of the query map section.
      Sha1_Map      : Util.Strings.Name_Access;

      --  The first query defined for that file.
      Queries       : Query_Definition_Access;

      --  The next XML query file registered in the application.
      Next          : Query_File_Access;

      --  The unique file index.
      File          : File_Index := 0;
   end record;

   type Context is new ADO.SQL.Query with record
      First      : Natural := 0;
      Last       : Natural := 0;
      Last_Index : Natural := 0;
      Max_Row_Count : Natural := 0;
      Query_Def  : Query_Definition_Access := null;
      Is_Count   : Boolean := False;
   end record;

   use Ada.Strings.Unbounded;

   --  SQL query pattern
   type Query_Pattern is limited record
      SQL : Ada.Strings.Unbounded.Unbounded_String;
   end record;

   type Query_Pattern_Array is array (Driver_Index) of Query_Pattern;

   type Query_Info is new Util.Refs.Ref_Entity with record
      Main_Query  : Query_Pattern_Array;
      Count_Query : Query_Pattern_Array;
   end record;
   type Query_Info_Access is access all Query_Info;

   package Query_Info_Ref is new Util.Refs.References (Query_Info, Query_Info_Access);
   package Atomic_Query_Info_Ref is new Query_Info_Ref.IR.Atomic;

   type Query_Info_Ref_Access is access all Atomic_Query_Info_Ref.Atomic_Ref;

   subtype Query_Index_Table is Query_Index range 1 .. Query_Index'Last;
   subtype File_Index_Table is File_Index range 1 .. File_Index'Last;

   type Query_File_Info is record
      --  Query absolute path name (after path resolution).
      Path          : Ada.Strings.Unbounded.Unbounded_String;

      --  File
      File          : Query_File_Access;

      --  Stamp when the query file will be checked.
      Next_Check    : Interfaces.Unsigned_32;

      --  Stamp identifying the modification date of the query file.
      Last_Modified : Interfaces.Unsigned_32;
   end record;

   --  Find the query with the given name.
   --  Returns the query definition that matches the name or null if there is none
   function Find_Query (File : in Query_File_Info;
                        Name : in String) return Query_Definition_Access;

   type Query_Table is array (Query_Index_Table range <>) of Query_Info_Ref.Ref;
   type Query_Table_Access is access all Query_Table;

   type File_Table is array (File_Index_Table range <>) of Query_File_Info;
   type File_Table_Access is access all File_Table;

   type Query_Manager is limited new Ada.Finalization.Limited_Controlled with record
      Driver  : Driver_Index := Driver_Index'First;
      Queries : Query_Table_Access;
      Files   : File_Table_Access;
   end record;

   overriding
   procedure Finalize (Manager : in out Query_Manager);

end ADO.Queries;
